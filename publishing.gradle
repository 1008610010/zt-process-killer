apply plugin: 'maven-publish'

publishing {
  publications {
    pluginMaven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom.withXml {
        // ewwww, fix the runtime deps into compile deps
        asNode().dependencies.'*'.findAll() {
          it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
            dep.name == it.artifactId.text()
          }
        }.each() {
          it.scope*.value = 'compile'
        }

        // add extra metadata into the POM
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          packaging 'jar'
          name 'ZT Process Killer'
          description 'A library for stopping external processes from Java.'
          url 'https://github.com/zeroturnaround/zt-process-killer'
          scm {
            url 'https://github.com/zeroturnaround/zt-process-killer'
            connection 'scm:git:git@github.com:zeroturnaround/zt-process-killer.git'
            developerConnection 'scm:git:git@github.com:zeroturnaround/zt-process-killer.git'
          }
          issueManagement {
            system 'GitHub'
            url 'https://github.com/zeroturnaround/zt-process-killer/issues'
          }
          developers {
            developer {
              id 'rein'
              name 'Rein'
              email 'rein@zeroturnaround.com'
            }
            developer {
              id 'toomasr'
              name 'Toomas'
              email 'toomas@zeroturnaround.com'
            }
          }
          licenses {
            license {
              name 'Apache 2.0 License'
              url 'https://www.apache.org/licenses/LICENSE-2.0'
              distribution 'repo'
            }
          }
        }
      }
    }
  }
}
