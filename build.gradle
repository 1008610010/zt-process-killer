buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'pink.madis.gradle:localprops:0.0.2'
  }
}

apply plugin: 'java'
apply plugin: 'pink.madis.gradle.localprops'

def versionStr = file('version.txt').text.trim()
allprojects {
  group 'org.zeroturnaround'
  version = versionStr

  repositories {
    mavenCentral()
  }
}

File getJavaExecutable(int version) {
  new File(project.localprops.get("java" + version + ".home") + "/bin/java")
}

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6
}

dependencies {
  compile(project(':api'))
  compile(project(':java6'))
  compile(project(':java8'))
  compile(project(':java9'))

  compile "org.zeroturnaround:zt-exec:1.7"
  compile "net.java.dev.jna:jna:4.2.2"
  compile "org.apache.commons:commons-lang3:3.5"
  compile "commons-io:commons-io:2.2"
  compile "org.slf4j:slf4j-api:1.7.6"

  testCompile "junit:junit:4.11"
  testCompile "ch.qos.logback:logback-classic:1.1.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

build {
  dependsOn sourcesJar, javadocJar
}

apply from: file('publishing.gradle')

task testJava16(type: Test) { executable = getJavaExecutable(6) } // EOL, unsupported on Travis
task testJava17(type: Test) { executable = getJavaExecutable(7) }
task testJava18(type: Test) { executable = getJavaExecutable(8) }

check {
  dependsOn 'testJava17'
  dependsOn 'testJava18'
}
